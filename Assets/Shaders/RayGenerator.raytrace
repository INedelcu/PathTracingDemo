
#include "UnityShaderVariables.cginc"
#include "GlobalResources.hlsl"
#include "Utils.hlsl"
#include "RayPayload.hlsl"

#pragma max_recursion_depth 1

// Input
float                           g_Zoom;
float                           g_AspectRatio;
uint                            g_FrameIndex;
TextureCube<float4>             g_EnvTex;
SamplerState                    sampler_g_EnvTex;

// Output
RWTexture2D<float4>             g_Radiance;

[shader("raygeneration")]
void MainRayGenShader()
{ 
    uint2 launchIndex = uint2(DispatchRaysIndex().x, DispatchRaysDimensions().y - DispatchRaysIndex().y - 1);
    uint2 launchDim = DispatchRaysDimensions().xy;

    // Shoot rays through the center of a pixel.
    float2 frameCoord = launchIndex + float2(0.5, 0.5);

    // Initial random number generator seed for this pixel. The rngState will change every time we draw a random number.
    uint rngState = uint(uint(launchIndex.x) * uint(1973) + uint(launchIndex.y) * uint(9277) + uint(g_FrameIndex) * uint(26699)) | uint(1);   

    float2 ndcCoords = frameCoord  / float2(launchDim.x - 1, launchDim.y - 1);
        
    ndcCoords = ndcCoords * 2 - float2(1, 1);
    ndcCoords = ndcCoords * g_Zoom;

    // Get a ray in view space.
    float3 viewDirection = normalize(float3(ndcCoords.x * g_AspectRatio, ndcCoords.y, 1));

    // Rotate the ray from view space to world space.
    float3 rayDirection = mul((float3x3)unity_CameraToWorld, viewDirection);

    uint missShaderIndex = 0;
    
    RayDesc ray;
    ray.Origin      = _WorldSpaceCameraPos;
    ray.Direction   = rayDirection;
    ray.TMin        = 0;
    ray.TMax        = K_T_MAX;
              
    RayPayload payload;
    payload.albedo                  = float3(1, 1, 1);
    payload.emission                = float3(0, 0, 0);
    payload.rngState                = rngState;
    payload.bounceIndexOpaque       = 0;
    payload.bounceIndexTransparent  = 0;
    payload.bounceRayOrigin         = float3(0, 0, 0);
    payload.bounceRayDirection      = float3(0, 0, 0);
    payload.lastWorldNormal         = float3(0, 0, 0);
    payload.k                       = 1;

    // Safe net for when shaders have compilation errors and don't increase the bounce index, resulting in an infinite loop.
    uint safeNet = 0;

    float3 radiance = float3(0, 0, 0);
    float3 throughput = float3(1, 1, 1);
        
    do
    {
        TraceRay(g_AccelStruct, 0, 0xFF, 0, 1, missShaderIndex, ray, payload);

        RayPayload payLoadShadow;
        payLoadShadow.bounceIndexOpaque       = 0;
        payLoadShadow.emission                = float3(0,0,0);

        if(payload.bounceIndexOpaque == -1)
        {
            payload.emission = g_EnvTex.SampleLevel(sampler_g_EnvTex, ray.Direction, 0).xyz;
            //payload.emission = float3(0,0,0);

            radiance += payload.emission * throughput;
            break;
        }
        else
        {
            // Accumulate emission from hit object
            radiance += payload.emission * throughput;
            
            throughput *= payload.albedo / max(0.001, payload.k);
            
            // Directionnal light sampling
            //float3 dirLightColor = float3(0,0,0);
            float3 dirLightColor = float3(0.5, 0.5, 0.5);
            float3 dirLight = normalize(float3(0,-1,1));
            
            RayDesc shadowRay;
            shadowRay.Origin      = payload.bounceRayOrigin;
            shadowRay.Direction   = -dirLight;
            shadowRay.TMin        = 0;
            shadowRay.TMax        = K_T_MAX;
            
            // Trace shadow ray
            TraceRay(g_AccelStruct, RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH, 0xFF, 0, 1, missShaderIndex, shadowRay, payLoadShadow);

            if(payLoadShadow.bounceIndexOpaque == -1)
            {
                payLoadShadow.emission                = saturate(dot(-dirLight,payload.lastWorldNormal)) * dirLightColor;
                radiance += payLoadShadow.emission * throughput;
            }
        }

        float pathStopProbability = 1;

#define ENABLE_RUSSIAN_ROULETTE 1

#if ENABLE_RUSSIAN_ROULETTE
        pathStopProbability = max(throughput.r, max(throughput.g, throughput.b));

        // Dark colors have higher chance to terminate the path early.
        if (pathStopProbability < RandomFloat01(payload.rngState))
            break;
#endif

        throughput *= 1 / pathStopProbability;

        ray.Origin      = payload.bounceRayOrigin;
        ray.Direction   = payload.bounceRayDirection;
    }
    while ((payload.bounceIndexOpaque <= g_BounceCountOpaque) && (payload.bounceIndexTransparent <= g_BounceCountTransparent) && (++safeNet < 1000));  

    g_Radiance[launchIndex] = float4(radiance, 1);
}

[shader("miss")]
void MainMissShader0(inout RayPayload payload : SV_RayPayload)
{
    //payload.emission                = g_EnvTex.SampleLevel(sampler_g_EnvTex, WorldRayDirection(), 0).xyz;
    payload.bounceIndexOpaque       = -1;
}
